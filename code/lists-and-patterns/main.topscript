#silent true;;
#use "topfind";;
#thread;;
#camlp4o;;
#require "core";;
#require "core.top";;
#require "ppx_jane";;
#silent false;;
[@@@part "0.5"];;
open Core_kernel;;
[1;2;3];;
[%%expect ocaml {|- : int list = [1; 2; 3]|}];;
[@@@part "1"];;
1 :: (2 :: (3 :: [])) ;;
[%%expect ocaml {|- : int list = [1; 2; 3]|}];;
1 :: 2 :: 3 :: [] ;;
[%%expect ocaml {|- : int list = [1; 2; 3]|}];;
[@@@part "2"];;
let empty = [];;
[%%expect ocaml {|val empty : 'a list = []|}];;
3 :: empty;;
[%%expect ocaml {|- : int list = [3]|}];;
"three" :: empty;;
[%%expect ocaml {|- : string list = ["three"]|}];;
[@@@part "3"];;
let l = 1 :: 2 :: 3 :: [];;
[%%expect ocaml {|val l : int list = [1; 2; 3]|}];;
let m = 0 :: l;;
[%%expect ocaml {|val m : int list = [0; 1; 2; 3]|}];;
l;;
[%%expect ocaml {|- : int list = [1; 2; 3]|}];;
[@@@part "4"];;
let rec sum l =
  match l with
  | [] -> 0
  | hd :: tl -> hd + sum tl
;;
[%%expect ocaml {|val sum : int list -> int = <fun>|}];;
sum [1;2;3];;
[%%expect ocaml {|- : int = 6|}];;
sum [];;
[%%expect ocaml {|- : int = 0|}];;
[@@@part "5"];;
let rec drop_value l to_drop =
  match l with
  | [] -> []
  | to_drop :: tl -> drop_value tl to_drop
  | hd :: tl -> hd :: drop_value tl to_drop
;;
[%%expect{|
Characters 106-114:
Warning 11: this match case is unused.
|};
ocaml {|val drop_value : 'a list -> 'a -> 'a list = <fun>|}];;
[@@@part "6"];;
drop_value [1;2;3] 2;;
[%%expect ocaml {|- : int list = []|}];;
[@@@part "7"];;
let rec drop_value l to_drop =
  match l with
  | [] -> []
  | hd :: tl ->
    let new_tl = drop_value tl to_drop in
    if hd = to_drop then new_tl else hd :: new_tl
;;
[%%expect ocaml {|val drop_value : 'a list -> 'a -> 'a list = <fun>|}];;
drop_value [1;2;3] 2;;
[%%expect ocaml {|- : int list = [1; 3]|}];;
[@@@part "8"];;
let rec drop_zero l =
  match l with
  | [] -> []
  | 0  :: tl -> drop_zero tl
  | hd :: tl -> hd :: drop_zero tl
;;
[%%expect ocaml {|val drop_zero : int list -> int list = <fun>|}];;
drop_zero [1;2;0;3];;
[%%expect ocaml {|- : int list = [1; 2; 3]|}];;
[@@@part "9"];;
let plus_one_match x =
  match x with
  | 0 -> 1
  | 1 -> 2
  | 2 -> 3
  | 3 -> 4
  | 4 -> 5
  | 5 -> 6
  | _ -> x + 1
;;
[%%expect ocaml {|val plus_one_match : int -> int = <fun>|}];;
let plus_one_if x =
  if      x = 0 then 1
  else if x = 1 then 2
  else if x = 2 then 3
  else if x = 3 then 4
  else if x = 4 then 5
  else if x = 5 then 6
  else x + 1
;;
[%%expect ocaml {|val plus_one_if : int -> int = <fun>|}];;
[@@@part "10"];;
#require "core_bench";;
[%%expect{|
/Users/yminsky/.opam/4.04.2/lib/textutils: added to search path
/Users/yminsky/.opam/4.04.2/lib/textutils/textutils.cma: loaded
/Users/yminsky/.opam/4.04.2/lib/core_bench: added to search path
/Users/yminsky/.opam/4.04.2/lib/core_bench/core_bench.cma: loaded
|}];;
open Core_bench;;
[ Bench.Test.create ~name:"plus_one_match" (fun () ->
      ignore (plus_one_match 10))
; Bench.Test.create ~name:"plus_one_if" (fun () ->
      ignore (plus_one_if 10)) ]
|> Bench.bench
;;
[%%expect.nondeterministic{|
Estimated testing time 20s (2 benchmarks x 10s). Change using -quota SECS.
┌────────────────┬──────────┐
│ Name           │ Time/Run │
├────────────────┼──────────┤
│ plus_one_match │  34.86ns │
│ plus_one_if    │  54.89ns │
└────────────────┴──────────┘
|};
ocaml {|- : unit = ()|}];;
[@@@part "11"];;
let rec sum_if l =
  if List.is_empty l then 0
  else List.hd_exn l + sum_if (List.tl_exn l)
;;
[%%expect ocaml {|val sum_if : int list -> int = <fun>|}];;
[@@@part "12"];;
let numbers = List.range 0 1000 in
[ Bench.Test.create ~name:"sum_if" (fun () -> ignore (sum_if numbers))
; Bench.Test.create ~name:"sum"    (fun () -> ignore (sum numbers)) ]
|> Bench.bench
;;
[%%expect.nondeterministic{|
Estimated testing time 20s (2 benchmarks x 10s). Change using -quota SECS.
┌────────┬──────────┐
│ Name   │ Time/Run │
├────────┼──────────┤
│ sum_if │  62.00us │
│ sum    │  17.99us │
└────────┴──────────┘
|};
ocaml {|- : unit = ()|}];;
[@@@part "13"];;
let rec drop_zero l =
  match l with
  | [] -> []
  | 0  :: tl -> drop_zero tl
;;
[%%expect{|
Characters 24-78:
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a case that is not matched:
1::_
|};
ocaml {|val drop_zero : int list -> 'a list = <fun>|}];;
[@@@part "14"];;
List.map ~f:String.length ["Hello"; "World!"];;
[%%expect ocaml {|- : int list = [5; 6]|}];;
[@@@part "15"];;
List.map2_exn ~f:Int.max [1;2;3] [3;2;1];;
[%%expect ocaml {|- : int list = [3; 2; 3]|}];;
[@@@part "16"];;
List.map2_exn ~f:Int.max [1;2;3] [3;2;1;0];;
[%%expect{|Exception: (Invalid_argument "length mismatch in map2_exn: 3 <> 4 ").|}];;
[@@@part "17"];;
List.fold;;
[%%expect ocaml {|- : 'a list -> init:'accum -> f:('accum -> 'a -> 'accum) -> 'accum = <fun>|}];;
[@@@part "18"];;
List.fold ~init:0 ~f:(+) [1;2;3;4];;
[%%expect ocaml {|- : int = 10|}];;
[@@@part "19"];;
List.fold ~init:[] ~f:(fun list x -> x :: list) [1;2;3;4];;
[%%expect ocaml {|- : int list = [4; 3; 2; 1]|}];;
[@@@part "20"];;
let max_widths header rows =
  let lengths l = List.map ~f:String.length l in
  List.fold rows
    ~init:(lengths header)
    ~f:(fun acc row ->
        List.map2_exn ~f:Int.max acc (lengths row))
;;
[%%expect ocaml {|val max_widths : string list -> string list list -> int list = <fun>|}];;
[@@@part "21"];;
let render_separator widths =
  let pieces = List.map widths
      ~f:(fun w -> String.make (w + 2) '-')
  in
  "|" ^ String.concat ~sep:"+" pieces ^ "|"
;;
[%%expect ocaml {|val render_separator : int list -> string = <fun>|}];;
render_separator [3;6;2];;
[%%expect ocaml {|- : string = "|-----+--------+----|"|}];;
[@@@part "22"];;
let s = "." ^ "."  ^ "."  ^ "."  ^ "."  ^ "."  ^ ".";;
[%%expect ocaml {|val s : string = "......."|}];;
[@@@part "23"];;
let s = String.concat [".";".";".";".";".";".";"."];;
[%%expect ocaml {|val s : string = "......."|}];;
[@@@part "24"];;
let pad s length =
  " " ^ s ^ String.make (length - String.length s + 1) ' '
;;
[%%expect ocaml {|val pad : string -> int -> string = <fun>|}];;
pad "hello" 10;;
[%%expect ocaml {|- : string = " hello      "|}];;
[@@@part "25"];;
let render_row row widths =
  let padded = List.map2_exn row widths ~f:pad in
  "|" ^ String.concat ~sep:"|" padded ^ "|"
;;
[%%expect ocaml {|val render_row : string list -> int list -> string = <fun>|}];;
render_row ["Hello";"World"] [10;15];;
[%%expect ocaml {|- : string = "| Hello      | World           |"|}];;
[@@@part "26"];;
let render_table header rows =
  let widths = max_widths header rows in
  String.concat ~sep:"\n"
    (render_row header widths
     :: render_separator widths
     :: List.map rows ~f:(fun row -> render_row row widths)
    )
;;
[%%expect ocaml {|val render_table : string list -> string list list -> string = <fun>|}];;
[@@@part "27"];;
List.reduce;;
[%%expect ocaml {|- : 'a list -> f:('a -> 'a -> 'a) -> 'a option = <fun>|}];;
[@@@part "28"];;
List.reduce ~f:(+) [1;2;3;4;5];;
[%%expect ocaml {|- : int option = Some 15|}];;
List.reduce ~f:(+) [];;
[%%expect ocaml {|- : int option = None|}];;
[@@@part "29"];;
List.filter ~f:(fun x -> x mod 2 = 0) [1;2;3;4;5];;
[%%expect ocaml {|- : int list = [2; 4]|}];;
[@@@part "30"];;
let extensions filenames =
  List.filter_map filenames ~f:(fun fname ->
      match String.rsplit2 ~on:'.' fname with
      | None  | Some ("",_) -> None
      | Some (_,ext) ->
        Some ext)
  |> List.dedup
;;
[%%expect ocaml {|val extensions : string list -> string list = <fun>|}];;
extensions ["foo.c"; "foo.ml"; "bar.ml"; "bar.mli"];;
[%%expect ocaml {|- : string list = ["c"; "ml"; "mli"]|}];;
[@@@part "31"];;
let is_ocaml_source s =
  match String.rsplit2 s ~on:'.' with
  | Some (_,("ml"|"mli")) -> true
  | _ -> false
;;
[%%expect ocaml {|val is_ocaml_source : string -> bool = <fun>|}];;
let (ml_files,other_files) =
  List.partition_tf ["foo.c"; "foo.ml"; "bar.ml"; "bar.mli"]  ~f:is_ocaml_source;;
[%%expect ocaml {|
val ml_files : string list = ["foo.ml"; "bar.ml"; "bar.mli"]
val other_files : string list = ["foo.c"]
|}];;
[@@@part "32"];;
List.append [1;2;3] [4;5;6];;
[%%expect ocaml {|- : int list = [1; 2; 3; 4; 5; 6]|}];;
[@@@part "33"];;
[1;2;3] @ [4;5;6];;
[%%expect ocaml {|- : int list = [1; 2; 3; 4; 5; 6]|}];;
[@@@part "34"];;
List.concat [[1;2];[3;4;5];[6];[]];;
[%%expect ocaml {|- : int list = [1; 2; 3; 4; 5; 6]|}];;
[@@@part "35"];;
module Sys = Core.Sys
let rec ls_rec s =
  if Sys.is_file_exn ~follow_symlinks:true s
  then [s]
  else
    Sys.ls_dir s
    |> List.map ~f:(fun sub -> ls_rec (Filename.concat s sub))
    |> List.concat
;;
[%%expect ocaml {|
module Sys = Core.Sys
val ls_rec : string -> string list = <fun>
|}];;
[@@@part "36"];;
let rec ls_rec s =
  if Sys.is_file_exn ~follow_symlinks:true s
  then [s]
  else
    Sys.ls_dir s
    |> List.concat_map ~f:(fun sub -> ls_rec (Filename.concat s sub))
;;
[%%expect ocaml {|val ls_rec : string -> string list = <fun>|}];;
[@@@part "37"];;
let rec length = function
  | [] -> 0
  | _ :: tl -> 1 + length tl
;;
[%%expect ocaml {|val length : 'a list -> int = <fun>|}];;
length [1;2;3];;
[%%expect ocaml {|- : int = 3|}];;
[@@@part "38"];;
let make_list n = List.init n ~f:(fun x -> x);;
[%%expect ocaml {|val make_list : int -> int list = <fun>|}];;
length (make_list 10);;
[%%expect ocaml {|- : int = 10|}];;
length (make_list 10_000_000);;
[%%expect{|Stack overflow during evaluation (looping recursion?).|}];;
[@@@part "39"];;
let rec length_plus_n l n =
  match l with
  | [] -> n
  | _ :: tl -> length_plus_n tl (n + 1)
;;
[%%expect ocaml {|val length_plus_n : 'a list -> int -> int = <fun>|}];;
let length l = length_plus_n l 0 ;;
[%%expect ocaml {|val length : 'a list -> int = <fun>|}];;
length [1;2;3;4];;
[%%expect ocaml {|- : int = 4|}];;
[@@@part "40"];;
length (make_list 10_000_000);;
[%%expect ocaml {|- : int = 10000000|}];;
[@@@part "41"];;
let rec destutter list =
  match list with
  | [] -> []
  | [hd] -> [hd]
  | hd :: hd' :: tl ->
    if hd = hd' then destutter (hd' :: tl)
    else hd :: destutter (hd' :: tl)
;;
[%%expect ocaml {|val destutter : 'a list -> 'a list = <fun>|}];;
[@@@part "42"];;
let rec destutter = function
  | [] as l -> l
  | [_] as l -> l
  | hd :: (hd' :: _ as tl) ->
    if hd = hd' then destutter tl
    else hd :: destutter tl
;;
[%%expect ocaml {|val destutter : 'a list -> 'a list = <fun>|}];;
[@@@part "43"];;
let rec destutter = function
  | [] | [_] as l -> l
  | hd :: (hd' :: _ as tl) ->
    if hd = hd' then destutter tl
    else hd :: destutter tl
;;
[%%expect ocaml {|val destutter : 'a list -> 'a list = <fun>|}];;
[@@@part "44"];;
let rec destutter = function
  | [] | [_] as l -> l
  | hd :: (hd' :: _ as tl) when hd = hd' -> destutter tl
  | hd :: tl -> hd :: destutter tl
;;
[%%expect ocaml {|val destutter : 'a list -> 'a list = <fun>|}];;
[@@@part "45"];;
3 = 4;;
[%%expect ocaml {|- : bool = false|}];;
[3;4;5] = [3;4;5];;
[%%expect ocaml {|- : bool = true|}];;
[Some 3; None] = [None; Some 3];;
[%%expect ocaml {|- : bool = false|}];;
[@@@part "46"];;
(=);;
[%%expect ocaml {|- : 'a -> 'a -> bool = <fun>|}];;
[@@@part "47"];;
(fun x -> x + 1) = (fun x -> x + 1);;
[%%expect{|Exception: (Invalid_argument "compare: functional value").|}];;
[@@@part "48"];;
let rec count_some list =
  match list with
  | [] -> 0
  | x :: tl when Option.is_none x -> count_some tl
  | x :: tl when Option.is_some x -> 1 + count_some tl
;;
[%%expect{|
Characters 28-161:
Warning 8: this pattern-matching is not exhaustive.
Here is an example of a case that is not matched:
_::_
(However, some guarded clause may match this value.)
|};
ocaml {|val count_some : 'a option list -> int = <fun>|}];;
[@@@part "49"];;
count_some [Some 3; None; Some 4];;
[%%expect ocaml {|- : int = 2|}];;
[@@@part "50"];;
let rec count_some list =
  match list with
  | [] -> 0
  | x :: tl when Option.is_none x -> count_some tl
  | x :: tl when Option.is_some x -> 1 + count_some tl
  | x :: tl -> -1 (* unreachable *)
;;
[%%expect ocaml {|val count_some : 'a option list -> int = <fun>|}];;
[@@@part "51"];;
let rec count_some list =
  match list with
  | [] -> 0
  | x :: tl when Option.is_none x -> count_some tl
  | _ :: tl -> 1 + count_some tl
;;
[%%expect ocaml {|val count_some : 'a option list -> int = <fun>|}];;
[@@@part "52"];;
let rec count_some list =
  match list with
  | [] -> 0
  | None   :: tl -> count_some tl
  | Some _ :: tl -> 1 + count_some tl
;;
[%%expect ocaml {|val count_some : 'a option list -> int = <fun>|}];;
[@@@part "53"];;
let count_some l = List.count ~f:Option.is_some l;;
[%%expect ocaml {|val count_some : 'a option list -> int = <fun>|}];;
[@@@part "69"];;
printf "%s\n"
  (render_table
     ["language";"architect";"first release"]
     [ ["Lisp" ;"John McCarthy" ;"1958"] ;
       ["C"    ;"Dennis Ritchie";"1969"] ;
       ["ML"   ;"Robin Milner"  ;"1973"] ;
       ["OCaml";"Xavier Leroy"  ;"1996"] ;
     ]);;
[%%expect{|
| language | architect      | first release |
|----------+----------------+---------------|
| Lisp     | John McCarthy  | 1958          |
| C        | Dennis Ritchie | 1969          |
| ML       | Robin Milner   | 1973          |
| OCaml    | Xavier Leroy   | 1996          |
|};
ocaml {|- : unit = ()|}];;
