#silent true;;
#use "topfind";;
#thread;;
#require "core";;
#require "core.top";;
#require "ppx_jane";;
#silent false;;
open Base;;
[@@@part "1"];;
let memoize f =
  let memo_table = Hashtbl.Poly.create () in
  (fun x ->
     Hashtbl.find_or_add memo_table x ~default:(fun () -> f x))
;;
[%%expect ocaml {|val memoize : ('a -> 'b) -> 'a -> 'b = <fun>|}];;
[@@@part "2"];;
let rec edit_distance s t =
  match String.length s, String.length t with
  | (0,x) | (x,0) -> x
  | (len_s,len_t) ->
    let s' = String.drop_suffix s 1 in
    let t' = String.drop_suffix t 1 in
    let cost_to_drop_both =
      if Char.(=) s.[len_s - 1] t.[len_t - 1] then 0 else 1
    in
    List.reduce_exn ~f:Int.min
      [ edit_distance s' t  + 1
      ; edit_distance s  t' + 1
      ; edit_distance s' t' + cost_to_drop_both
      ]
;;
[%%expect ocaml {|val edit_distance : string -> string -> int = <fun>|}];;
edit_distance "OCaml" "ocaml";;
[%%expect ocaml {|- : int = 2|}];;
[@@@part "3"];;
let time f =
  let open Core in
  let start = Time.now () in
  let x = f () in
  let stop = Time.now () in
  printf "Time: %F ms\n" (Time.diff stop start |> Time.Span.to_ms);
  x 
;;
[%%expect ocaml {|val time : (unit -> 'a) -> 'a = <fun>|}];;
[@@@part "4"];;
time (fun () -> edit_distance "OCaml" "ocaml");;
[%%expect{|Time: 1.10292434692 ms|};
ocaml {|- : int = 2|}];;
time (fun () -> edit_distance "OCaml 4.01" "ocaml 4.01");;
[%%expect{|Time: 3282.86218643 ms|};
ocaml {|- : int = 2|}];;
[@@@part "5"];;
let memo_rec f_norec x =
  let fref = ref (fun _ -> assert false) in
  let f = memoize (fun x -> f_norec !fref x) in
  fref := f;
  f x
;;
[%%expect ocaml {|val memo_rec : (('a -> 'b) -> 'a -> 'b) -> 'a -> 'b = <fun>|}];;
[@@@part "6"];;
let edit_distance = memo_rec (fun edit_distance (s,t) ->
  match String.length s, String.length t with
  | (0,x) | (x,0) -> x
  | (len_s,len_t) ->
    let s' = String.drop_suffix s 1 in
    let t' = String.drop_suffix t 1 in
    let cost_to_drop_both =
      if Char.(=) s.[len_s - 1] t.[len_t - 1] then 0 else 1
    in
    List.reduce_exn ~f:Int.min
      [ edit_distance (s',t ) + 1
      ; edit_distance (s ,t') + 1
      ; edit_distance (s',t') + cost_to_drop_both
      ]) ;;
[%%expect ocaml {|val edit_distance : string * string -> int = <fun>|}];;
[@@@part "7"];;
time (fun () -> edit_distance ("OCaml 4.01","ocaml 4.01"));;
[%%expect{|Time: 0.348091125488 ms|};
ocaml {|- : int = 2|}];;
