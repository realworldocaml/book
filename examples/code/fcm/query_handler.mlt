#use "topfind";;
#thread;;
#require "core";;
#require "ppx_jane";;
#install_printer Core.Sexp.pp_hum;;
#install_printer Core.Error.pp;;
open Base;;
[@@@part "0.5"];;
module type Query_handler = sig

  (** Configuration for a query handler.  Note that this can be
      converted to and from an s-expression *)
  type config [@@deriving sexp]

  (** The name of the query-handling service *)
  val name : string

  (** The state of the query handler *)
  type t

  (** Creates a new query handler from a config *)
  val create : config -> t

  (** Evaluate a given query, where both input and output are
      s-expressions *)
  val eval : t -> Sexp.t -> Sexp.t Or_error.t
end;;
[%%expect ocaml {|
module type Query_handler =
  sig
    type config
    val config_of_sexp : Sexp.t -> config
    val sexp_of_config : config -> Sexp.t
    val name : string
    type t
    val create : config -> t
    val eval : t -> Sexp.t -> Sexp.t Or_error.t
  end
|}];;
[@@@part "1"];;
module type M = sig type t [@@deriving sexp] end;;
[%%expect ocaml {|
module type M =
  sig type t val t_of_sexp : Sexp.t -> t val sexp_of_t : t -> Sexp.t end
|}];;
[@@@part "2"];;
type u = { a: int; b: float } [@@deriving sexp];;
[%%expect ocaml {|
type u = { a : int; b : float; }
val u_of_sexp : Sexp.t -> u = <fun>
val sexp_of_u : u -> Sexp.t = <fun>
|}];;
sexp_of_u {a=3;b=7.};;
[%%expect ocaml {|- : Sexp.t = ((a 3) (b 7))|}];;
u_of_sexp (Core_kernel.Sexp.of_string "((a 43) (b 3.4))");;
[%%expect ocaml {|- : u = {a = 43; b = 3.4}|}];;
[@@@part "3"];;
module Unique = struct
  type config = int [@@deriving sexp]
  type t = { mutable next_id: int }

  let name = "unique"
  let create start_at = { next_id = start_at }

  let eval t sexp =
    match Or_error.try_with (fun () -> unit_of_sexp sexp) with
    | Error _ as err -> err
    | Ok () ->
      let response = Ok (Int.sexp_of_t t.next_id) in
      t.next_id <- t.next_id + 1;
      response
end;;
[%%expect ocaml {|
module Unique :
  sig
    type config = int
    val config_of_sexp : Sexp.t -> config
    val sexp_of_config : config -> Sexp.t
    type t = { mutable next_id : config; }
    val name : string
    val create : config -> t
    val eval : t -> Sexp.t -> (Sexp.t, Error.t) result
  end
|}];;
[@@@part "4"];;
let unique = Unique.create 0;;
[%%expect ocaml {|val unique : Unique.t = {Unique.next_id = 0}|}];;
Unique.eval unique (Sexp.List []);;
[%%expect ocaml {|- : (Sexp.t, Error.t) result = Ok 0|}];;
Unique.eval unique (Sexp.List []);;
[%%expect ocaml {|- : (Sexp.t, Error.t) result = Ok 1|}];;
[@@@part "5"];;
module List_dir = struct
  type config = string [@@deriving sexp]
  type t = { cwd: string }

  (** [is_abs p] Returns true if [p] is an absolute path  *)
  let is_abs p =
    String.length p > 0 && Char.(=) p.[0] '/'

  let name = "ls"
  let create cwd = { cwd }

  let eval t sexp =
    match Or_error.try_with (fun () -> string_of_sexp sexp) with
    | Error _ as err -> err
    | Ok dir ->
      let dir =
        if is_abs dir then dir
        else Core.Filename.concat t.cwd dir
      in
      Ok (Array.sexp_of_t String.sexp_of_t (Core.Sys.readdir dir))
end;;
[%%expect ocaml {|
module List_dir :
  sig
    type config = string
    val config_of_sexp : Sexp.t -> config
    val sexp_of_config : config -> Sexp.t
    type t = { cwd : config; }
    val is_abs : config -> bool
    val name : config
    val create : config -> t
    val eval : t -> Sexp.t -> (Sexp.t, Error.t) result
  end
|}];;
[@@@part "6"];;
let list_dir = List_dir.create "/var";;
[%%expect ocaml {|val list_dir : List_dir.t = {List_dir.cwd = "/var"}|}];;
List_dir.eval list_dir (sexp_of_string ".");;
[%%expect ocaml {|
- : (Sexp.t, Error.t) result =
Ok
 (yp networkd install empty ma mail spool jabberd vm msgs audit root lib db
  at log folders netboot run rpc tmp backups agentx rwho)
|}];;
List_dir.eval list_dir (sexp_of_string "yp");;
[%%expect.nondeterministic ocaml {|- : (Sexp.t, Error.t) result = Ok (binding)|}];;
[@@@part "7"];;
module type Query_handler_instance = sig
  module Query_handler : Query_handler
  val this : Query_handler.t
end;;
[%%expect ocaml {|
module type Query_handler_instance =
  sig module Query_handler : Query_handler val this : Query_handler.t end
|}];;
[@@@part "8"];;
let unique_instance =
  (module struct
    module Query_handler = Unique
    let this = Unique.create 0
  end : Query_handler_instance);;
[%%expect ocaml {|val unique_instance : (module Query_handler_instance) = <module>|}];;
[@@@part "9"];;
let build_instance
      (type a)
      (module Q : Query_handler with type config = a)
      config
  =
  (module struct
    module Query_handler = Q
    let this = Q.create config
  end : Query_handler_instance)
;;
[%%expect ocaml {|
val build_instance :
  (module Query_handler with type config = 'a) ->
  'a -> (module Query_handler_instance) = <fun>
|}];;
[@@@part "10"];;
let unique_instance = build_instance (module Unique) 0;;
[%%expect ocaml {|val unique_instance : (module Query_handler_instance) = <module>|}];;
let list_dir_instance = build_instance (module List_dir)  "/var";;
[%%expect ocaml {|val list_dir_instance : (module Query_handler_instance) = <module>|}];;
[@@@part "11"];;
let build_dispatch_table handlers =
  let table = Hashtbl.create (module String) () in
  List.iter handlers
    ~f:(fun ((module I : Query_handler_instance) as instance) ->
      Hashtbl.set table ~key:I.Query_handler.name ~data:instance);
  table
;;
[%%expect ocaml {|
val build_dispatch_table :
  (module Query_handler_instance) list ->
  (string, (module Query_handler_instance)) Hashtbl.t = <fun>
|}];;
[@@@part "12"];;
let dispatch dispatch_table name_and_query =
  match name_and_query with
  | Sexp.List [Sexp.Atom name; query] ->
    begin match Hashtbl.find dispatch_table name with
    | None ->
      Or_error.error "Could not find matching handler"
        name String.sexp_of_t
    | Some (module I : Query_handler_instance) ->
      I.Query_handler.eval I.this query
    end
  | _ ->
    Or_error.error_string "malformed query"
;;
[%%expect ocaml {|
val dispatch :
  (string, (module Query_handler_instance)) Hashtbl.t ->
  Sexp.t -> Sexp.t Or_error.t = <fun>
|}];;
[@@@part "13"];;
open Stdio;;
let rec cli dispatch_table =
  printf ">>> %!";
  let result =
    match In_channel.input_line In_channel.stdin with
    | None -> `Stop
    | Some line ->
      match Or_error.try_with (fun () ->
        Core_kernel.Sexp.of_string line)
      with
      | Error e -> `Continue (Error.to_string_hum e)
      | Ok (Sexp.Atom "quit") -> `Stop
      | Ok query ->
        begin match dispatch dispatch_table query with
        | Error e -> `Continue (Error.to_string_hum e)
        | Ok s    -> `Continue (Sexp.to_string_hum s)
        end;
  in
  match result with
  | `Stop -> ()
  | `Continue msg ->
    printf "%s\n%!" msg;
    cli dispatch_table
;;
[%%expect ocaml {|val cli : (string, (module Query_handler_instance)) Hashtbl.t -> unit = <fun>|}];;
[@@@part "14"];;
type query_handler_instance = { name : string
                              ; eval : Sexp.t -> Sexp.t Or_error.t
                              };;
[%%expect ocaml {|
type query_handler_instance = {
  name : string;
  eval : Sexp.t -> Sexp.t Or_error.t;
}
|}];;
type query_handler = Sexp.t -> query_handler_instance
;;
[%%expect ocaml {|type query_handler = Sexp.t -> query_handler_instance|}];;
[@@@part "15"];;
let unique_handler config_sexp =
  let config = Unique.config_of_sexp config_sexp in
  let unique = Unique.create config in
  { name = Unique.name
  ; eval = (fun config -> Unique.eval unique config)
  }
;;
[%%expect ocaml {|val unique_handler : Sexp.t -> query_handler_instance = <fun>|}];;
